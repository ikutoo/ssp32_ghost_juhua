///////// 文　ゴーストテンプレート
///////// 种々のイベント辞书
///////// written by umeici.

OnFileDrop2
{
	_filepath = SPLITPATH(reference0)
	_filename = _filepath[2]
	_fileext = TOLOWER(_filepath[3])
	case _fileext
	{
		when ".png", ".jpg", ".jpeg", ".bmp" 
		{
			Wallpaperfile = reference0
			"\0\s[20]%(_filename)%(_fileext)是吗。\w9\w9\s[0]\n\n[half]这个、\w9\w9要设定成桌布吗？\n\n/
			\q[◇置中,SetWallpaper_0]\n/
			\q[◇并列,SetWallpaper_1]\n/
			\q[◇放大,SetWallpaper_2]\n\n[half]/
			\q[◇取消,SetWallpaper_Cancel]\e"
		}
		when ".mid", ".wav", ".mp3", ".wma", ".au", ".ogg"
		{
			Musicfile = reference0
			"\0\s[20]%(_filename)%(_fileext)是吗。\w9\w9\s[0]\n\n要听这首音乐吗？\n\n/
			\q[◇通常播放,MusicStart_0]\n/
			\q[◇重复播放,MusicStart_1]\n\n[half]/
			\q[◇取消,MusicStart_Cancel]\e"
		}
		others
		{
			"\0\s[0]%(_filename)%(_fileext)\e"
		}
	
	}
}

Select.SetWallpaper_0
{
	_i = FUNCTIONEX("saori\proxy.dll", "set_wall_paper.exe", "%(ASC(34))%(Wallpaperfile)%(ASC(34))", "0")
	"\0\s[0]嗯…\w9\w9\n像这样子吗？\e"
}
Select.SetWallpaper_1
{
	_i = FUNCTIONEX("saori\proxy.dll", "set_wall_paper.exe", "%(ASC(34))%(Wallpaperfile)%(ASC(34))", "1")
	"\0\s[0]嗯…\w9\w9\n像这样子吗？\e"
}
Select.SetWallpaper_2
{
	_i = FUNCTIONEX("saori\proxy.dll", "set_wall_paper.exe", "%(ASC(34))%(Wallpaperfile)%(ASC(34))", "2")
	"\0\s[0]嗯…\w9\w9\n像这样子吗？\e"
}
Select.SetWallpaper_Cancel
{
	"\0\s[0]那么、\w9停止变更壁纸。\e"
}

Select.MusicStart_0
{
	_i = FUNCTIONEX("saori\mciaudior.dll", "stop")
	_j = FUNCTIONEX("saori\mciaudior.dll", "load", "%(Musicfile)")
	_k = FUNCTIONEX("saori\mciaudior.dll", "play")
	"\0\s[0]好的、\w9那么开始播放。\e"
}
Select.MusicStart_1
{
	_i = FUNCTIONEX("saori\mciaudior.dll", "stop")
	_j = FUNCTIONEX("saori\mciaudior.dll", "load", "%(Musicfile)")
	_k = FUNCTIONEX("saori\mciaudior.dll", "loop")
	"\0\s[0]好的、\w9那么开始播放。\e"
}
Select.MusicStart_Cancel
{
	"\0\s[0]好的。\e"
}


//**** インストール関连 *****************************************************************

//---- インストール中のメッセージ -------------------------------------------------------
OnInstallBegin
{
	"\0\s[20]嗯…\e"
	"\0\s[20]唔…\e"
}

//---- インストール完了メッセージ -------------------------------------------------------
OnInstallComplete
{
	if reference0 == "shell"
	{
		"\0\s[0]Shell『%(reference1)』\n\n[half]支给完成。\e"
	}
	elseif reference0 == "ghost"
	{
		"\0\s[0]人格『%(reference1)』\n\n[half]配置确认。\e"
		"\0\s[0]人格『%(reference1)』\n\n[half]投入确认。\e"
	}
	elseif reference0 == "balloon"
	{
		"\0\s[0]对话框『%(reference1)』\n\n[half]追加完成。\e"
	}
	elseif reference0 == "plugin"
	{
		"\0\s[0]套件『%(reference1)』\n\n[half]支给完成。\e"
	}
}

//---- インストール失败 -----------------------------------------------------------------------------------------------

OnInstallFailure
{
	"\0\s[3]好像失败了…\w9\w9\1\s[10]原因是：%(reference0)。\e"
}

OnInstallRefuse
{
	"\0\s[20]这是…\w9\w9\n『%(reference0)收』。\w9\s[0]\n请直接交给本人。\e"
}


//**** 消灭 ***********************************************************************************************************

OnVanishSelecting
{
	"\0\s[3]…\w9…\w9…\e"
}

OnVanishSelected
{
	"\0\s[3]是这样吗…\w9\w9\s[1]\n啊、那个…\w9\w9\s[26]我不会忘记%(username)的。\w9\w9\s[1]\n\n[half]所以…\w9如果%(username)可以的话、也请…\w9\s[2]\n啊、\w9\w9\s[1]对不起、\w9\w9没什么。\w9\w9\1\s[10]…\w9…\w9…\w9\w9\0\s[40]\n\n[half]那么、请保重。\w9\w9\1\s[10]\n\n[half]那。\w9\n有机会的话、再见吧。\w9\w9\w9\e"
}

OnVanishCancel
{
	talkmemo = "e_0"
	"\0\s[3]…\w9请不要吓我…\e"
}

TalkVanishButtonHold
{
	"\0\s[3]…\w9继续待在这里也可以吗？"
}

OnVanished
{
	"\1\s[10]\0\s[6]对不起…\w9\w9\w5\n让%(username)费事了。\e"
}


//**** メールチェック *************************************************************************************************

OnBIFFBegin
{
	"\0\s[0]那么、查询邮件。\n\n[half]\_q伺服器：%(reference2)\_q\e"
}

OnBIFFComplete
{
	if reference0 == 0
	{
		"\0\s[20]嗯…\w9\w9\s[0]\n什么都没有的样子。\e"
		"\0\s[20]嗯…\w9\w9\s[0]\n0件。\e"
	}
	else
	{
		"\0\s[0]来了%(reference0)通邮件喔。\n\n[half]\_q邮件的总容量：%(reference1) Bytes\_q\e"
	}
}

OnBIFFFailure
{
	if reference0 == "timeout"
	{
		"\0\s[0]唔～\w9\w9\w9\n无法顺利完成。\w9\w9\n连线速度似乎太慢了。\e"
	}
	elseif reference0 == "kick"
	{
		"\0\s[0]唔～\w9\w9\w9\n无法顺利完成。\w9\w9\s[20]\n密码正确吗？\e"
	}
	elseif reference0 == "defect"
	{
		"\0\s[0]唔～\w9\w9\w9\n无法顺利完成。\w9\w9\s[20]\n有正确的设定吗？\e"
	}
	else
	{
		"\0\s[0]唔～\w9\w9\w9\n无法顺利完成。\e"
	}
}


//**** ネットワーク更新 *****************************************************************

//---- ネットワーク更新开始 -------------------------------------------------------------
OnUpdateBegin
{
	"\0\s[0]更新开始。\e"
}

//---- ネットワーク更新准备完了 ---------------------------------------------------------
OnUpdateReady
{
	"\0\s[6]有%(reference0)个更新。\e"
}

OnUpdateComplete
{
	if reference0 == "none"
	{
		"\0\s[0]好像没有更新。\e"
	}
	else
	{
		"\0\s[0]结束了。\e"
	}
}

OnUpdateFailure
{
	if reference0 == "timeout"
	{
		"\0\s[3]连线逾时。\w9\w9\s[0]\n更新停止了。\e"
	}
	elseif reference0 == "md5 miss"
	{
		"\0\s[3]MD5不一致。\w9\w9\s[0]\n更新中止。\e"
	}
	else
	{
		"\0\s[3]error404：找不到档案。\w9\w9\s[0]\n更新停止了。\e"
	}
}

//---- ネットワーク更新 ダウンロード开始 ------------------------------------------------
OnUpdate.OnDownloadBegin
{
	"\0\s[0]\_q%(reference0)\_q\e"
}

//---- ネットワーク更新 MD5値照合开始 ---------------------------------------------------
OnUpdate.OnMD5CompareBegin
{
	"\0\s[0]ＭＤ5确认中。\e"
}

//---- ネットワーク更新 MD5値一致 -------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	"\0\s[0]\_q%(reference1)\n%(reference2)\_q\n\n[half]一致。\e"
}

//---- ネットワーク更新 MD5値不一致 -----------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	"\0\s[0]\_q%(reference1)\n%(reference2)\_q\s[20]\n\n[half]嗯？"
}

//**** ヘッドラインセンス ***************************************************************

//---- ヘッドラインセンス开始 -----------------------------------------------------------
OnHeadlinesenseBegin
{
	"\0\s[0]『%(reference0)』 的Headline确认…\e"
}

//---- ヘッドライン読み上げ -------------------------------------------------------------
OnHeadlinesense.OnFind
{
	//---- はじめのセリフ
	if reference2 == "First" || reference2 == "First and Last"
	{
		//---- 最初のページ
		"\0\b2\s[0]『%(reference0)』的Headline。"
	}
	elseif reference2 == "Next"
	{
		//---- 真ん中あたりのページ
		"\0\b2\s[0]『%(reference0)』的Headline。"
	}
	elseif reference2 == "Last"
	{
		//---- 最后のページ
		"\0\b2\s[0]『%(reference0)』的Headline。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%(reference3)\n\n"
	--
	//---- 选択肢
	if reference2 == "First" || reference2 == "Next"
	{
		//---- 次のページがある场合
		"\q0[][◇次页]/
		\q1[%reference1][◇读取]/
		\q2[HEADLINECANCEL][◇取消]\e"
	}
	else
	{
		//---- 次のページがない场合
		"\q0[%reference1][◇读取]/
		\q1[HEADLINECANCEL][◇取消]\e"
	}
}

//---- ヘッドライン更新が无かった -------------------------------------------------------
OnHeadlinesenseComplete
{
	"\0\s[0]没有更新。\e"
}

//---- ヘッドラインセンス失败 -----------------------------------------------------------
OnHeadlinesenseFailure
{
	"\0\s[6]唔～\w9\w9\w9\s[3]\n无法顺利完成。\w9\n"
	--
	if reference0 == "can't download"
	{
		"下载失败。"
	}
	elseif reference0 == "can't analyze"
	{
		"解析失败。"
	}
	else
	{
		"对不起。"
	}
	--
	"\e"
}


//**** ファイル作成関连 *****************************************************************

//---- updates2.dau作成开始 -------------------------------------------------------------
OnUpdatedataCreating
{
	"\0\s[0]要是能顺利做出『update2.dau』档案就好了呢。\e"
}

//---- updates2.dau作成完了 -------------------------------------------------------------
OnUpdatedataCreated
{
	"\0\s[0]嗯。\w9\w9\n完成了。\e"
}

//---- NAR作成开始 ----------------------------------------------------------------------
OnNarCreating
{
	"\0\s[0]要是能顺利做出『nar』档案就好了呢。\e"
}

//---- NAR作成完了 ----------------------------------------------------------------------
OnNarCreated
{
	"\0\s[0]好的。\w9\w9\n完成了。\e"
}

//****　时计合わせ　*********************************************************************

//----　时计合わせ开始 ------------------------------------------------------------------
OnSNTPBegin
{
	"\0\s[0]那么、\w9开始查询正确时间。\e"
}

//----　时刻取得成功、比较开始 ----------------------------------------------------------
OnSNTPCompare
{
	"\0\s[0]/
	current %reference1[0]/%reference1[1]/%reference1[2] %reference1[3]:%reference1[4]:%reference1[5]\w2\n/
	local   %reference2[0]/%reference2[1]/%reference2[2] %reference2[3]:%reference2[4]:%reference2[5]\w2\n/
	\n"
	--
	if reference3 == 0
	{
		"\0\s[0]已经对准了喔。\e"
	}
	else
	{
		"\0\n\s[0]误差%(reference3)秒。\w9\w9\s[0]\n调整成正确时间。\6\e"
	}
}

//----　时计合わせ失败 ------------------------------------------------------------------
OnSNTPFailure
{
	"\0\s[3]无法确认…\e"
}


//**** そのほか *************************************************************************

//---- ディスプレイ解像度変更 -----------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情报を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference0
	displaywidth  = reference1
	displayheight = reference2
}

//---- URLドロップ ----------------------------------------------------------------------
//---- CROW でゴーストインストーラからゴーストをインストールを始めた时にも呼ばれます ----
OnURLDropping
{
	"\0\s[6]下载中…\n\n[half]\_qＵＲＬ：%(reference0)\_q\e"
}
/*
//---- 起动时に MATERIA を検出 ----------------------------------------------------------
OnEmbryoExist
{
	"\0\s[0]以前的「伪春菜」起动了呢ー。\e"
}

//---- 起动时に猫どりふを検出 -----------------------------------------------------------
OnNekodorifExist
{
	"\0\s[0]うわ、猫どりふが‥‥\w9\1\s[10]‥\w5‥\5！！\e"
}

//---- スクリーンセーバー実行开始 -------------------------------------------------------
OnScreenSaverStart
{
	"\0\s[0]萤幕保护程式开始工作了\e"
}

//---- スクリーンセーバー実行终了 -------------------------------------------------------
OnScreenSaverEnd
{
	"\0\s[0]%username、欢迎回来。\e"
}

*/